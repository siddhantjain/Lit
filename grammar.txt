1. <Program> ->  <O_functions> <main function>
2. <O_functions> -> <function><O_functions>|e
3. <main function> -> TK_MAIN<stmts>TK_END
4. <stmts> -><dclrtv_stmts> <O_stmts><return_stmt>|e
5. <O_stmts> -> <stmt><O_stmts>|e
6. <stmt> ->  <cond_stmt>|<iterative_stmt>|<assgnm_stmt>|<gen_stmt>|<funcall_stmt>|<break_stmt>
7. <gen_stmt> ->  <print_stmts>|<read_stmt>
8. <print_stmts>-> <print_stmt>|<println_stmt>
9. <print_stmt>-> TK_PRINT TK_ORD<all_var>TK_CRD TK_SCLN
10. <println_stmt>-> TK_PRINTLN TK_ORD<all_var>TK_CRD TK_SCLN
11. <all_var> -> TK_NUM|TK_RNUM|TK_STRLIT|<id>
12. <id> -> TK_ID <id_1’>
13. <id_1’> -> TK_OSQ <id_2’>|e
14. <id_2’> -> TK_ID TK_CSQ| TK_NUM TK_CSQ
15. <read_stmt> -> TK_READ TK_ORD <id> TK_CRD TK_SCLN
16. <dclrtv_stmts> -> <dclrtv_stmt><dclrtv_stmts>|e
17. <dclrtv_stmt> -> <type> TK_ID <d_s’>
18. <d_s’> -> <global_stmt>TK_SCLN|TK_OSQ TK_NUM TK_CSQ  TK_SCLN
19. <global_stmt> -> TK_GLOBAL|e
20. <type> -> TK_INT|TK_FLOAT
21. <arithmetic_expression>-><term><a_e’>
22. <a_e’>->TK_PLUS<term><a_e’>|TK_MINUS<term><a_e’>|e
23. <term>-><factor><t_e’>
24. <t_e’>-> TK_MUL <factor><t_e’>|TK_DIV<factor><t_e’>|e
25. <factor>->TK_ORD<arithmetic_expression>TK_CRD|<var>
26. <var> -> TK_NUM|TK_RNUM|<id>
27. <Boolean_expression> -> TK_ORD <Boolean_expression> TK_CRD <logical_operator> TK_ORD <Boolean_expression> TK_CRD|<var><b_e’> 
28. <b_e’> -> <relational_operator><var><b_e’>|e
29. <Boolean_expression> -> TK_NOT <Boolean_expression>
30. <logical_operator> -> TK_AND|TK_OR
31. <relational_operator> -> TK_EQ|TK_LTE|TK_LESS|TK_GRTR|TK_GTE|TK_NOTEQ
32. <cond_stmt> -> TK_IF TK_ORD <Boolean_expression> TK_CRD TK_THEN <O_stmts> <c_s’>
33. <c_s’> -> TK_ENDIF| TK_ELSE <O_stmts> TK_ENDIF
34. <iterative_stmt> -> TK_WHILE TK_ORD <Boolean_expression> TK_CRD  <O_stmts> TK_EWHILE
35. <funcall_Stmt>->TK_CALL  TK_FUNC TK_OSQ <list_var>TK_CSQ TK_SCLN
36. <list_var>-> <id> <next>
37. <next>->TK_COMMA <list_var>|e
38. <break_stmt> -> TK_BREAK TK_SCLN 
39. <assgnm_stmt>-> <id> TK_ASSGN <arithmetic_expression>TK_SCLN
40. <assgnm_stmt>->TK_OSQ<list_var>TK_CSQ TK_ASSGN <funcall_Stmt>
41. <function>->TK_FUNC <input_parameters><output_parameters> TK_SCLN <stmts> TK_END
42. <input_parameters> -> TK_IPP TK_OSQ <i_p’>
43. <i_p’> -> <parameter_list> TK_CSQ | TK_VOID TK_CSQ
44. <output_parameters> -> TK_OPP TK_OSQ <o_p’>
45. <o_p’> -><parameter_list> TK_CSQ| TK_VOID TK_CSQ
46. <parameter_list> -> <type> TK_ID <nextpl>
47. <nextpl> -> TK_COMMA <parameter_list>|e
48.  <return_stmt> -> TK_RET <r_s’>
49. <r_s’> -> TK_OSQ <list_var> TK_CSQ TK_SCLN| TK_VOID TK_SCLN
